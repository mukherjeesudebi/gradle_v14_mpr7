plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.16'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'com.vaadin' version '0.14.8.1'
}

group = 'org.gradle.samples'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    compile.canBeResolved = true // workaround for Vaadin 8 plugin
    runtime.canBeResolved = true // workaround for Vaadin 8 plugin

    configureEach {
        resolutionStrategy {
            force 'org.jsoup:jsoup:1.15.3',
                    'com.vaadin:vaadin-server:7.7.41',
                    'com.vaadin:vaadin-client:7.7.41','com.vaadin:vaadin-themes:7.7.41'
        }
    }
}

dependencies {
    implementation project(':sample-ui')
    implementation platform('com.vaadin:vaadin-bom:14.8.1')
    implementation('com.vaadin:vaadin-spring-boot-starter') {
        ["com.vaadin.webjar", "org.webjars.bowergithub.insites",
         "org.webjars.bowergithub.polymer", "org.webjars.bowergithub.polymerelements",
         "org.webjars.bowergithub.vaadin", "org.webjars.bowergithub.webcomponents"]
                .each { group -> exclude group: group }
    }
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

vaadin {
    pnpmEnable = true
}

springBoot {
    mainClass = 'org.gradle.samples.SpringBootDemoApplication'
}

task copyWebappFiles(type: Copy) {
    println "Copying resource"

    from project(':sample-ui').file('src/main/webapp/VAADIN')
    into 'src/main/resources/VAADIN'
}

processResources.dependsOn copyWebappFiles

description = 'app'
